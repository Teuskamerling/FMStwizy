%%***************************************************************************************
%% file         sfcn_timeout_init.tlc
%% brief        Target Language Compiler file that contains the code generation specifics
%%              for an S-function with the same name.
%%
%%---------------------------------------------------------------------------------------
%%                          C O P Y R I G H T
%%---------------------------------------------------------------------------------------
%%  Copyright 2019 (c) by HAN Automotive     http://www.han.nl     All rights reserved
%%
%%---------------------------------------------------------------------------------------
%%                            L I C E N S E
%%---------------------------------------------------------------------------------------
%% Permission is hereby granted, free of charge, to any person obtaining a copy of this
%% software and associated documentation files (the "Software"), to deal in the Software
%% without restriction, including without limitation the rights to use, copy, modify, merge,
%% publish, distribute, sublicense, and/or sell copies of the Software, and to permit
%% persons to whom the Software is furnished to do so, subject to the following conditions:
%%
%% The above copyright notice and this permission notice shall be included in all copies or
%% substantial portions of the Software.
%%
%% THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
%% INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
%% PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE
%% FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR
%% OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
%% DEALINGS IN THE SOFTWARE.
%%
%%***************************************************************************************
%implements  sfcn_timeout_init "C"

%% add required include files
%<LibAddToCommonIncludes("timeout.h")>


%% Function: BlockTypeSetup ==========================================================
%%
%% Purpose:
%%      Code generation for configuration
%%
%function BlockTypeSetup(block, system) void
  %% Generate the function call subsystem function prototype.
  %openfile tmpBuf
  void TimeoutOverflowIRQ_%<block.RTWdata.moduleID>(void);
  %closefile tmpBuf
  %% write it to the model's source file.
  %assign srcFile = LibGetModelDotCFile()
  %<LibSetSourceFileSection(srcFile, "UserTop", tmpBuf)>

%endfunction


%% Function: Start ==========================================================
%%
%% Purpose:
%%      Code generation for initialization
%%
%function Start(block, system) Output
  %% The TimeoutInitModule() function must be called before any other functions of
  %% the TimeOut library are called. Therefore call TimeoutInitModule in 
  %% BlockTypeSetup as apposed to Start !!! originally in BlockTypeSetup!!!.
  %<LibMdlStartCustomCode("/* initialize the timer module for output compare. */\n", "execution")>
  %<LibMdlStartCustomCode("TimeoutInitModule(%<block.RTWdata.moduleID>, %<block.RTWdata.frequency>);\n", "execution")>
  %%
  %%
  /* register the free running counter overflow callback handler */
  TimeoutRegisterOverflowCallback(%<block.RTWdata.moduleID>, TimeoutOverflowIRQ_%<block.RTWdata.moduleID>);
  %%
%endfunction



%% Function: Outputs ==========================================================
%%
%% Purpose:
%%      Code generation for signal output
%%
%function Outputs(block, system) Output
%%
  %% Generate the function call subsystem as a seperate function.
  %openfile tmpBuf
  void TimeoutOverflowIRQ_%<block.RTWdata.moduleID>(void)
  {
    %foreach callIdx = NumSFcnSysOutputCalls
      %if LibIsEqual(SFcnSystemOutputCall[callIdx].BlockToCall, "unconnected")
        %continue
      %endif
      %% call the downstream system
      %<LibBlockExecuteFcnCall(block, callIdx)>\
    %endforeach    
  }
  %closefile tmpBuf
  %% write it to the model's source file.
  %assign srcFile = LibGetModelDotCFile()
  %<LibSetSourceFileSection(srcFile, "Functions", tmpBuf)>
%%
%endfunction


%%******************************* end of sfcn_timeout_init.tlc ******************

