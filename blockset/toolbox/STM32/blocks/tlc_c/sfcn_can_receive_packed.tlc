%%***************************************************************************************
%% file         sfcn_can_receive_packed.tlc
%% brief        Target Language Compiler file that contains the code generation specifics
%%              for an S-function with the same name.
%%
%%---------------------------------------------------------------------------------------
%%                          C O P Y R I G H T
%%---------------------------------------------------------------------------------------
%%  Copyright 2019 (c) by HAN Automotive     http://www.han.nl     All rights reserved
%%
%%---------------------------------------------------------------------------------------
%%                            L I C E N S E
%%---------------------------------------------------------------------------------------
%% Permission is hereby granted, free of charge, to any person obtaining a copy of this
%% software and associated documentation files (the "Software"), to deal in the Software
%% without restriction, including without limitation the rights to use, copy, modify, merge,
%% publish, distribute, sublicense, and/or sell copies of the Software, and to permit
%% persons to whom the Software is furnished to do so, subject to the following conditions:
%%
%% The above copyright notice and this permission notice shall be included in all copies or
%% substantial portions of the Software.
%%
%% THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
%% INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
%% PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE
%% FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR
%% OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
%% DEALINGS IN THE SOFTWARE.
%%
%%***************************************************************************************
%implements  sfcn_can_receive_packed "C"


%% add required include files
%<LibAddToCommonIncludes("canio.h")>

%% Function: BlockInstanceSetup ==========================================================
%%
%% Purpose:
%%      Code generation for configuration
%%
%function BlockInstanceSetup(block, system) void
  %% get the name of the model's source file.
  %assign srcFile = LibGetModelDotCFile()

  %% create a buffer that can be used for storing message data.
  %<LibSetSourceFileSection(srcFile, "Declarations", "static CAN_DATATYPE canRxPacked_%<block.RTWdata.canBus>_%<block.RTWdata.canID>;\n")>
  
%endfunction


%% Function: Start ==========================================================
%%
%% Purpose:
%%      Code generation for initialization
%%
%function Start(block, system) Output
{
  /* register a CAN storage buffer for the message with this ID */
  CanIoCreateMessageReceivedStorage(%<block.RTWdata.canBus>, %<block.RTWdata.canID>u);
}
%endfunction


%% Function: Outputs ==========================================================
%%
%% Purpose:
%%      Code generation for signal output
%%
%function Outputs(block, system) Output
  /* read the message data and store the new parameter */
  %<LibBlockOutputSignal(0 , "", "", 0)> = CanIoGetMessage(%<block.RTWdata.canBus>, %<block.RTWdata.canID>u, &canRxPacked_%<block.RTWdata.canBus>_%<block.RTWdata.canID>.Length, &canRxPacked_%<block.RTWdata.canBus>_%<block.RTWdata.canID>.Data[0], NULL);
  
  /* process the message data */  
  if (%<LibBlockOutputSignal(0 , "", "", 0)> > 0)
  {
    canRxPacked_%<block.RTWdata.canBus>_%<block.RTWdata.canID>.ID = %<block.RTWdata.canID>u;
    canRxPacked_%<block.RTWdata.canBus>_%<block.RTWdata.canID>.Extended = 0;
    if ((canRxPacked_%<block.RTWdata.canBus>_%<block.RTWdata.canID>.ID & CAN_EXTID_MASK) != 0)
    {
      canRxPacked_%<block.RTWdata.canBus>_%<block.RTWdata.canID>.ID &= ~CAN_EXTID_MASK;
      canRxPacked_%<block.RTWdata.canBus>_%<block.RTWdata.canID>.Extended = 1;
    }
    %<LibBlockOutputSignal(1 , "", "", 0)> = canRxPacked_%<block.RTWdata.canBus>_%<block.RTWdata.canID>;
  }  
%endfunction


%%******************************* end of sfcn_can_receive_packed.tlc ********************





